<html>
<head>
    <title>WebGl Thing</title>
    <script src="bower_components/lodash/dist/lodash.js" type="text/javascript"></script>
    <script src="driver.js" type="text/javascript"></script>
    <style>
        * {
            margin:0;
            padding:0;
        }
        .stats table * {
            color: cornflowerblue;
            font-family: Sans-Serif;
        }
    </style>
    <script id="shader-vs" type="x-shader/x-vertex">
precision mediump float;
attribute vec2 vertPosition;
void main() {
  gl_Position = vec4(vertPosition, 0.0, 1.0);
}
    </script>
    <script id="shader-fs" type="x-shader/x-fragment">
#extension GL_OES_standard_derivatives : enable

#ifdef GL_ES
precision mediump float;
#endif


uniform float time;
uniform vec2 resolution;
const int wavesLength = ${wavesLength};
uniform vec4 waves[wavesLength]; // [x,y,time,hue]

const float PI = 3.1415926535897932384626433832795;
const float PI2 = 2.0 * PI;

/** cycle smoothly from 0 to 1 over the specified period in seconds */
float timeCycle(void) {
    const float period = 3.0;
    float smooth = .5 * (1.0 + sin(time * PI2/period));
    return smooth;
}

/** convert hsv to rgb. from: http://stackoverflow.com/questions/15095909/from-rgb-to-hsv-in-opengl-glsl */
vec3 hsv2rgb(vec3 c) {
    vec4 K = vec4(1.0, 2.0 / 3.0, 1.0 / 3.0, 3.0);
    vec3 p = abs(fract(c.xxx + K.xyz) * 6.0 - K.www);
    return c.z * mix(K.xxx, clamp(p - K.xxx, 0.0, 1.0), c.y);
}

/** color via sin wave tamped by decaying exponential */
vec3 attenuatedWaveColor(float distance, float startTime, float hue) {
    float decayRate = -4.0;
    float scale = .5;
    float offset = 1.0;
    float stretch = 40.0 + distance;
    float cycle = (startTime + time) * 5.0;

    float decay = exp(decayRate * distance);
    float wiggle = scale * (offset + cos(distance * stretch - cycle)); 
    float value = wiggle * decay;
    value /= float(wavesLength / 2);
    float saturation = .7;
    vec3 rgb = hsv2rgb(vec3(hue, saturation, value));
    return rgb;
}


/** color from one wave */
vec3 colorContrib(vec4 center) {
    vec2 fromCenter = abs(center.xy - gl_FragCoord.xy / resolution.xy);
    float distance = length(fromCenter);
    return attenuatedWaveColor(distance, center.z, center.w);
}

/** accumulate a color from all the waves for this pixel. */
void main( void ) {
    vec3 color = vec3(0.0, 0.0, 0.0);
    for (int i =0; i < wavesLength; i++) {
        color += colorContrib(waves[i]);
    }

    gl_FragColor = vec4( color, 1.0 );
}

    </script>
</head>

<body onload="start();">
    <div class="stats" style="position:fixed; left:10; top: 10;">
        <table>
            <tr> <td> Frame rate:</td> 
                 <td> <span id="frameRate"> 0</span></td>
            </tr>
        </table>
    </div>
    <canvas id="shader"></canvas>
</body>

</html>
